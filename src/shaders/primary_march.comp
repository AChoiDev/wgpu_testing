#version 460 core
layout (local_size_x = 8, local_size_y = 8, local_size_z = 1) in;

//p1 => padding1
//TODO: Rename this to frame
layout(set = 0, binding = 5) uniform Frame {
    vec3 pos;
      int p0;
    vec3 right;             
      int p1;
    vec3 up;                
      int p2;
    vec3 forward;           
      int p3;
    float cotanHalfFov;
      int p5; int p6; int p7;
    ivec2 renderResolution;
      int p8; int p9;
};

layout(set = 0, binding = 0, r32f) uniform image2D depth;
layout(set = 0, binding = 2) uniform sampler monoBitMapSampler;
layout(set = 0, binding = 3) uniform utexture3D monoBitMap;

vec2 getUV(ivec2 invoc) {
    vec2 resolution = vec2(renderResolution);
    float ratio = resolution.x / resolution.y;

    vec2 pixelCoords =  vec2(invoc) + vec2(0.5);
    
    return (2.0 / resolution.y) * pixelCoords - vec2(ratio, 1.0);
}

vec3 getRayDirection(ivec2 invoc) {
    vec3 baseDir = normalize(vec3(getUV(invoc), cotanHalfFov));

    return baseDir.x * right + baseDir.y * up + baseDir.z * forward;
}

int getBitIndex(ivec3 coords) {
  return 
      (coords.x & 1) 
    | ((coords.y & 1) << 1) 
    | ((coords.z & 1) << 2);
}

uint extractMonoBitMap(ivec3 coords, int lod) {
  int bitIndex = getBitIndex(coords);

  uint texelValue = texelFetch(usampler3D(monoBitMap, monoBitMapSampler), coords >> 1, lod).r;
  
  //get the nth bit
  return (texelValue >> bitIndex) & 1;
}

bool checkVoxel(ivec3 coords, int lod) {
  return extractMonoBitMap(coords >> lod, lod) == 1;
}

float mincomp(vec3 value) {
    return min(min(value.x, value.z), value.y);
}

bool insideBox3D(vec3 v, vec3 bottomLeft, vec3 topRight) {
    vec3 s = step(bottomLeft, v) - step(topRight, v);
    return (s.x * s.y * s.z) == 1.0; 
}

float getInputDepth(ivec2 invoc) {
  ivec2 coneCoords = invoc / 8;

  ivec2 coneDepthDims = (renderResolution + ivec2(7)) / 8;

  int pixelIndex = coneCoords.x + coneCoords.y * coneDepthDims.x;
  
  ivec2 readLocation = ivec2(pixelIndex % renderResolution.x, renderResolution.y + (pixelIndex / renderResolution.x));

  
  return imageLoad(depth, readLocation).r;
}


vec3 PlaneMarch(vec3 p0, vec3 d, out bool hit) {
  hit = false;
  float t = getInputDepth(ivec2(gl_GlobalInvocationID.xy)) - 0.00001;
  int i = 0;
  
  vec3 invDir = vec3(1.0 / d.x, 1.0 / d.y, 1.0 / d.z);
  vec3 corner = step(0, d);

  while (i <= 200) {
    i++;
    vec3 p = p0 + d * t;

    if (!insideBox3D(p, vec3(0), vec3(32))) {
      return p;
    }

    if (checkVoxel(ivec3(p), 0)) {
      hit = true;
      return p;
    }
    
    vec3 deltas = (corner - fract(p)) * invDir;
    t += mincomp(deltas) + 0.001;
  }

  return vec3(0);
}


void main() {
    ivec2 invoc = ivec2(gl_GlobalInvocationID.xy);


    // imageStore(depth, invoc, vec4(getInputDepth(invoc)));
    // return;

    bool hit;
    vec3 endPos = PlaneMarch(pos, getRayDirection(invoc), hit);
    if (!hit) {
      imageStore(depth, invoc, vec4(40.0));
      return;
    }

    float rayLength = length(endPos - pos);

    imageStore(depth, invoc, vec4(rayLength));
}

